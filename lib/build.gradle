plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.0.6' apply false
}

version = '1.0.0'
group = 'com.github.lehadnk'

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.5'
    implementation 'software.amazon.awssdk:s3:2.18.41'

    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    intTestImplementation 'org.testcontainers:localstack:1.17.6'
    intTestRuntimeOnly 'com.amazonaws:aws-java-sdk-s3:1.12.367'
    intTestImplementation 'org.assertj:assertj-core:3.24.2'
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.5'
    intTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
    withSourcesJar()
}

jar {
    enabled = true
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifactId = 'spring-s3'
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/lehadnk/spring-s3"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}